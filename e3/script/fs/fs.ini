Z" MSXML2.DOMDocument.3.0" create-object
[IF] .( Location: http://www.eserv.ru/ru/xml3sp4/) CR CR \EOF [THEN] DROP

S" config.f" INCLUDED

WINAPI: SetCurrentDirectoryA KERNEL32.DLL
WINAPI: GetCurrentDirectoryA KERNEL32.DLL

: GetDir
  PAD DUP 500 GetCurrentDirectoryA
;
: SetDir ( addr u -- )
  " {GetDir}\{s}" STR@ DROP SetCurrentDirectoryA DROP
;
: RANDOM-ID
  GetTickCount " web-{n}" STR@
;
: Default? { a u -- flag }
  a u R/O OPEN-FILE-SHARED
  IF DROP a u +ModuleDirName R/O OPEN-FILE-SHARED
     IF DROP FALSE EXIT 
     ELSE CLOSE-FILE THROW a u +ModuleDirName THEN
  ELSE CLOSE-FILE THROW a u THEN
  EVAL-FILE
  OVER 8 S" Content-" COMPARE 0<> IF HTML THEN TYPE TRUE
;
: r 1 ;
: sortby 0 ;
S" " S" ercode" SetParam
: md5pass 
  S" f_PASS" IsSet
  IF
    S" f_PASS" EVALUATE DUP 32 <>
    IF MD5 S" f_PASS" SetParam
    ELSE 2DROP THEN
  THEN
;

: GetKey
  [CHAR] ; PARSE OVER C@ [CHAR] " =
  IF 2 - 0 MAX SWAP 1+ SWAP THEN
;
USER uDelFlag
WINAPI: CopyFileA KERNEL32.DLL

: DeleteKey?1 ( na nu va vu -- )
  S" on" COMPARE 0=
  IF CONVERT% uSParams @ STR@ COMPARE 0= IF TRUE uDelFlag ! THEN
  ELSE 2DROP THEN
;
: DeleteKey? ( addr u -- flag )
  uSParams S! FALSE uDelFlag !
  ['] DeleteKey?1 ForEachParam
  uDelFlag @
;
: deletekey { fa fu \ h s mem -- }
  fa fu " {s}.bak" STR@ DROP
  FALSE SWAP fa CopyFileA
  0= IF S" ошибка" 2DROP THEN
  fa fu
  R/W OPEN-FILE THROW -> h
  "" -> s
  1002 ALLOCATE THROW -> mem
  BEGIN
    mem 1000 h READ-LINE THROW
  WHILE
    mem OVER ['] GetKey EVALUATE-WITH DeleteKey?
    IF DROP ELSE mem SWAP " {s}{CRLF}" s S+ THEN
  REPEAT DROP
  s STR@ DUP S>D h RESIZE-FILE THROW
  0 0 h REPOSITION-FILE THROW
  h WRITE-FILE THROW
  h CLOSE-FILE THROW
  mem FREE THROW
  s STRFREE
;

USER uFields USER uValues

: to_insert1 { na nu va vu -- }
  na 2 S" f_" COMPARE 0=
  IF na 2+ nu 2 - " {s}, "  uFields @ S+
     va vu 2DUP S" 0" COMPARE 0= 
     IF 2DROP " NULL, "
     ELSE " '{s}', " THEN uValues @ S+
  THEN
;
: to_insert ( -- addr u )
  S" " uFields S!
  S" " uValues S!
  md5pass
  ['] to_insert1 ForEachParam
  uValues @ STR@
  2- \ отрезали последний ", "
  0 MAX
  uFields @ STR@
  2- \ отрезали последний ", "
  0 MAX
  " insert into [{table}] ({s}) values ({s})" STR@
;
: XmlTable
  " select * from [{table}]" STR@ SqlQueryXml
;
: TxtDb { \ xml xmls xsl debug }
  S" debug" IsSet IF TRUE -> debug THEN
  debug IF TEXT ." <?xml version='1.0' encoding='windows-1251' ?><![CDATA[" 2DUP TYPE CR THEN
  S" table" SetParam
  S" Driver={Microsoft Text Driver (*.txt; *.csv)};DefaultDir=." SqlInit
\  S" Driver={Microsoft Text Driver (*.txt; *.csv)};hdr=yes;FMT=Delimited(;);DefaultDir=." SqlInit 
  debug IF ." ODBC init OK. Execute query... " CR THEN
  S" runsql" IsSet
  IF S" runsql" EVALUATE S" insert_" SEARCH NIP NIP
     IF
       to_insert SqlQueryXml 2DROP
       S" insert OK" S" ercode" SetParam
     THEN
     S" runsql" EVALUATE S" delete_" SEARCH NIP NIP
     IF
       S" table" EVALUATE deletekey
       S" delete OK" S" ercode" SetParam
     THEN
  THEN
  " {ModuleDirName}\dbtable.xml" STR@ EVAL-FILE
  debug IF ." OK. Length=" DUP . 2DUP TYPE CR THEN
  DROP -> xmls

  debug IF ." Loading XML table..." THEN
\  Z" Microsoft.XMLDOM" create-object THROW -> xml
  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xml
  arg( xmls _str )arg xml :: loadXML >
  debug IF . . ." OK" CR ELSE 2DROP THEN

  debug IF ." Loading XSL file..." THEN
\  Z" Microsoft.XMLDOM" create-object THROW -> xsl
  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xsl

  arg( " {ModuleDirName}\dbtable.xsl" STR@ DROP _str )arg xsl :: load >
  debug IF . . ." OK" CR ELSE 2DROP THEN

  debug IF ." XSL transform..." CR THEN
  arg( xsl _obj )arg xml :: transformNode >
  DROP ASCIIZ>
  debug IF ." OK. Length=" DUP . 2DUP TYPE CR ." ]]>" BYE THEN
;
USER uIniXml
USER uSectU
USER uSectA

: IniComment 2DROP ;

: 'IniFile@ ( S" key" S" section" S" file"  -- S" value" )
\ получить значение ключа из ini-файла без раскрытия {}
  { ka ku sa su fa fu \ mem }
  IniMaxString ALLOCATE THROW -> mem
  fa IniMaxString mem S" " DROP ka sa GetPrivateProfileStringA
  mem SWAP " {s}" STR@
  DUP
  IF
    S" s/(&)/&amp;/g" BregexpReplace DROP
    " {s}" STR@ BregexpFree
    S" s/(<)/&lt;/g" BregexpReplace DROP
    " {s}" STR@ BregexpFree
    S" s/(>)/&gt;/g" BregexpReplace DROP
    " {s}" STR@ BregexpFree
  THEN
  mem FREE THROW
;
: '(IniS@)
  File.Section[Key]>
  'IniFile@ DUP 0=
  IF 2DROP >IN 0! FileOrig.Section[Key]>
     'IniFile@
  THEN
;
: 'IniS@ ( a u -- S" value" )
  ['] '(IniS@) EVALUATE-WITH
;
: IniXml1 ( addr u -- )
  OVER C@ [CHAR] # = IF IniComment EXIT THEN
  2DUP "     <{s}" uIniXml @ S+
  2DUP uSectA @ uSectU @ IniDefault 'IniFile@ 
  DUP 0= IF 2DROP 2DUP uSectA @ uSectU @ IniDefaultOrig 'IniFile@
            S" >" uIniXml @ STR+ uIniXml @ STR+
         ELSE S"  file='user'>" uIniXml @ STR+ uIniXml @ STR+ THEN
       " </{s}>{CRLF}" uIniXml @ S+
;
: IniXmlSect ( addr u -- )
  2DUP uSectU ! uSectA !
  2DUP " <{s}>{CRLF}" uIniXml @ S+
  IniEnumXt @ >R
  0 0 2OVER IniDefaultOrig IniFile@ ['] IniXml1 IniEnum
  R> IniEnumXt !
  " </{s}>{CRLF}" uIniXml @ S+
;
: IniApplyParam { na nu va vu -- }
  na nu S" script_name" COMPARE 0= IF EXIT THEN
  na nu S" ercode" COMPARE 0= IF EXIT THEN
  na nu S" SectionName" COMPARE 0= IF EXIT THEN

  na nu " {SectionName}[{s}]" STR@ 'IniS@ \ нынешнее значение
  va vu COMPARE \ новое значение
  0<> IF va vu na nu " {SectionName}[{s}]" STR@ IniS! THEN \ записали, если изменилось
;
: IniApplyChanges
  ['] IniApplyParam ForEachParam
;
: IniXml { \ xml xmls xsl }
  S" SectionName" IsSet IF IniApplyChanges THEN
  " <?xml version='1.0' encoding='windows-1251' ?>{CRLF}<Ini Error='{ercode}'>{CRLF}" uIniXml !
  0 0 0 0 IniDefaultOrig IniFile@ ['] IniXmlSect IniEnum
  " </Ini>{CRLF}" uIniXml @ S+
  uIniXml @ STR@ DROP -> xmls

\  Z" Microsoft.XMLDOM" create-object THROW -> xml
  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xml
  arg( xmls _str )arg xml :: loadXML >
  2DROP

\  Z" Microsoft.XMLDOM" create-object THROW -> xsl
  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xsl
  arg( " inifile.xsl" STR@ DROP _str )arg xsl :: load >
  2DROP

  arg( xsl _obj )arg xml :: transformNode >
  DROP ASCIIZ>
;
: xul
  S" HTTP_USER_AGENT" ENVIRONMENT? 
  IF S" Gecko" SEARCH NIP NIP DROP FALSE
     IF S" xmlns='http://www.mozilla.org/keymaster/gatekeeper/there.is.only.xul'" EXIT THEN
  THEN S" "
;
: XulMenu { \ xml xmls xsl }
  " {ModuleDirName}\{LANG}\menu.xul" STR@ EVAL-FILE
  DROP -> xmls

  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xml
  arg( xmls _str )arg xml :: loadXML >
  2DROP

  Z" MSXML2.DOMDocument.3.0" create-object THROW -> xsl
  arg( " {ModuleDirName}\menu.xsl" STR@ DROP _str )arg xsl :: load >
  2DROP

  arg( xsl _obj )arg xml :: transformNode >
  DROP ASCIIZ>
;

: MainScript
  S" script_name" EVALUATE
  2DUP S" *.txt" WildCMP-U 0=
  IF TxtDb
  ELSE EVAL-FILE THEN
;
: Action2
  ALSO PARAMS
  MissingParams
  S" noskin" IsSet 
 IF
  S" script_name" EVALUATE Default? IF EXIT THEN
 ELSE
  " {LANG}\skin.html" STR@ Default? IF EXIT THEN
 THEN
;
' Action2 CGI-MAIN !

